module Main;

import Funim;
import Funim.Machine;

import MiscAlgos.Geometry;
import MiscAlgos.Functional;
import Math;
import Time;
import Cairo;
import Cairo.ImageSurface;

window_size : (I64, I64);
window_size = (1280, 720);

// 一つのスピログラフを見せるのに使う領域の大きさ
spirograph_size : F64;
spirograph_size = window_size.@1.to_F64 / 9.0 * 4.0 - 10.0;

// i番目のスピログラフを表示する（中心）位置
spirograph_center : I64 -> Vec2;
spirograph_center = |i| (
    let x = window_size.@0.to_F64 / 8.0 * (2 * (i % 4) + 1).to_F64;
    let y = window_size.@1.to_F64 / 4.0 * (2 * (i / 4) + 1).to_F64;
    Vec2::make(x, y) - Vec2::make_from_tuple(window_size.map01(to_F64)).scaled(0.5)
);

fps : F64;
fps = 60.0;

gear_color : (F64, F64, F64);
gear_color = (0.0, 1.0, 1.0);

pen_color : (F64, F64, F64);
pen_color = (1.0, 0.0, 0.0);

line_color : (F64, F64, F64, F64);
line_color = (1.0, 1.0, 1.0, 0.5);

velocity : F64;
velocity = 100.0;

// パラメータ曲線
type ParametricCurve = box struct {
    // 曲線の長さ、すなわちパラメータtの範囲
    length : F64,
    point : F64 -> Vec2,
    tangent : F64 -> Vec2,
};

namespace ParametricCurve {
    // パラメータ曲線の各点でのスピードを1にするように正規化する。
    // 最初の引数は精度
    normalize : I64 -> ParametricCurve -> ParametricCurve;
    normalize = |n, c| (
        // 元の曲線のパラメータ空間をn分割し、各iまでの長さを求めて配列に保存しておく。
        // point関数やtangent関数では、二分探索でiを求め、その点の位置や接ベクトルを返す。
        let cum_len = Array::fill(n, 0.0);
        let prev_pt = (c.@point)(0.0);
        let (cum_len, _) = range(1, n).fold((cum_len, prev_pt), |i, (cum_len, prev_pt)|
            let t = c.@length * i.to_F64 / n.to_F64;
            let now_pt = (c.@point)(t);
            let dl = (now_pt - prev_pt).@length;
            let cum_len = cum_len.set(i, cum_len.@(i - 1) + dl);
            (cum_len, now_pt)
        );
        let length = cum_len.@(n - 1);
        let l_to_u = |l| (
            let l = l.fmod(length);
            let l = if l < 0.0 { l + length } else { l };
            let i = cum_len.search_partition_point(|m| m < l);
            let t = c.@length * i.to_F64 / n.to_F64;
            t
        );
        let point = |t| (c.@point)(l_to_u(t));
        let tangent = |t| (
            let v = (c.@tangent)(l_to_u(t));
            v.scaled(1.0 / v.@length)
        );
        ParametricCurve {
            length : length,
            point : point,
            tangent : tangent,
        }
    );

    concatenate : ParametricCurve -> ParametricCurve -> ParametricCurve;
    concatenate = |c1, c2| (
        let length = c1.@length + c2.@length;
        let normalize_t = |t| let t = t.fmod(length); if t < 0.0 { t + length } else { t };
        let point = |t| let t = normalize_t(t); if t < c1.@length { (c1.@point)(t) } else { (c2.@point)(t - c1.@length) };
        let tangent = |t| let t = normalize_t(t); if t < c1.@length { (c1.@tangent)(t) } else { (c2.@tangent)(t - c1.@length) };
        ParametricCurve {
            length : length,
            point : point,
            tangent : tangent,
        }
    );

    invert : ParametricCurve -> ParametricCurve;
    invert = |c| (
        let length = c.@length;
        let point = |t| (c.@point)(-t);
        let tangent = |t| -(c.@tangent)(-t);
        ParametricCurve {
            length : length,
            point : point,
            tangent : tangent,
        }
    );
}

type Gear = box struct {
    // 周を表すパラメータ曲線。閉曲線でなければならない。任意のtにおける速さは1でなくてはならない。
    curve : ParametricCurve,
    // 固定ギアのとき、原点の座標。回転ギアのとき、軌跡を描く点の座標。
    point : Vec2, 
};

namespace Gear {
    render : Cairo -> Gear -> IO ();
    render = |cairo, gear| (
        let dt = 5.0;
        let n = (gear.@curve.@length / dt).to_I64 + 2; // 分割数
        cairo.set_source_rgb(gear_color);;
        range(0, n).fold_m((), |i, _|
            let t = i.to_F64 * dt;
            let p = (gear.@curve.@point)(t);
            if i == 0 {
                cairo.move_to(p.to_tuple)
            } else {
                cairo.line_to(p.to_tuple)
            }
        );;
        cairo.stroke
    );

    // 楕円ギアを作成する
    //
    // 半径は長軸長さ、短軸長さ、点
    ellipse_gear : F64 -> F64 -> Vec2 -> Gear;
    ellipse_gear = |a, b, p| (
        let curve = ParametricCurve {
            length : 2.0 * pi64,
            point : |t| Vec2::make(a * cos(t), b * sin(t)),
            tangent : |t| Vec2::make(-a * sin(t), b * cos(t)),
        }.normalize(1e4);
        Gear {
            curve : curve,
            point : p,
        }
    );

    // 楕円ギアを作成する（逆順）
    //
    // 半径は長軸長さ、短軸長さ、点
    ellipse_gear_rev : F64 -> F64 -> Vec2 -> Gear;
    ellipse_gear_rev = |a, b, p| (
        let curve = ParametricCurve {
            length : 2.0 * pi64,
            point : |t| Vec2::make(a * cos(-t), b * sin(-t)),
            tangent : |t| Vec2::make(a * sin(-t), -b * cos(-t)),
        }.normalize(1e4);
        Gear {
            curve : curve,
            point : p,
        }
    );

    // 円ギアを作成する
    // 
    // 引数は半径と点
    // 半径をマイナスにすると逆向きになる
    circle_gear : F64 -> Vec2 -> Gear;
    circle_gear = |r, p| (
        let curve = ParametricCurve {
            length : 2.0 * pi64 * r.abs,
            point : |t| Vec2::make(cos(t/r), sin(t/r)).scaled(r),
            tangent : |t| Vec2::make(-sin(t/r), cos(t/r)),
        };
        Gear {
            curve : curve,
            point : p,
        }
    );
}

type SpiroGraph = box struct {
    // 固定ギア
    fixed_gear : Gear,
    // 回転ギア
    rotating_gear : Gear,
};

namespace SpiroGraph {
    // 回転ギア座標から固定ギア座標への変換行列を求める
    get_transform : F64 -> SpiroGraph -> Matrix;
    get_transform = |t, spirograph| (
        let fixed_gear = spirograph.@fixed_gear;
        let rotating_gear = spirograph.@rotating_gear;
        
        // 固定ギアの接点と接ベクトル
        let p = (fixed_gear.@curve.@point)(t);
        let v = (fixed_gear.@curve.@tangent)(t);

        // 回転ギアの接点と接ベクトル
        let q = (rotating_gear.@curve.@point)(t);
        let w = (rotating_gear.@curve.@tangent)(t);

        // (q, w)を(p, v)に移すアフィン変換を求める。
        // まずは回転でwをvに一致させる。次に、その変換でqを変換したものをq2とし、平行移動でq2をpに一致させる。
        let angle = angle(w, v);
        let m0 = Matrix::init_rotate(angle);
        let q2 = Vec2::make_from_tuple $ m0.transform_point(q.to_tuple);
        let d = p - q2;
        let m = m0.translate(d.to_tuple);

        m
    );

    two_circles_outer : F64 -> F64 -> Vec2 -> SpiroGraph;
    two_circles_outer = |r1, r2, p| (
        let fixed_gear = circle_gear(r1, Vec2::make(0.0, 0.0));
        let rotating_gear = circle_gear(-r2, p);
        SpiroGraph {
            fixed_gear : fixed_gear,
            rotating_gear : rotating_gear,
        }
    );

    two_circles_inner : F64 -> F64 -> Vec2 -> SpiroGraph;
    two_circles_inner = |r1, r2, p| (
        let fixed_gear = circle_gear(r1, Vec2::make(0.0, 0.0));
        let rotating_gear = circle_gear(r2, p);
        SpiroGraph {
            fixed_gear : fixed_gear,
            rotating_gear : rotating_gear,
        }
    );

    circle_ellipse_outer : F64 -> F64 -> F64 -> Vec2 -> SpiroGraph;
    circle_ellipse_outer = |r1, a, b, p| (
        let fixed_gear = circle_gear(r1, Vec2::make(0.0, 0.0));
        let rotating_gear = ellipse_gear_rev(a, b, p);
        SpiroGraph {
            fixed_gear : fixed_gear,
            rotating_gear : rotating_gear,
        }
    );

    circle_ellipse_inner : F64 -> F64 -> F64 -> Vec2 -> SpiroGraph;
    circle_ellipse_inner = |r1, a, b, p| (
        let fixed_gear = circle_gear(r1, Vec2::make(0.0, 0.0));
        let rotating_gear = ellipse_gear(a, b, p);
        SpiroGraph {
            fixed_gear : fixed_gear,
            rotating_gear : rotating_gear,
        }
    );

    ellipse_circle_outer : F64 -> F64 -> F64 -> Vec2 -> SpiroGraph;
    ellipse_circle_outer = |a, b, r2, p| (
        let fixed_gear = ellipse_gear(a, b, Vec2::make(0.0, 0.0));
        let rotating_gear = circle_gear(-r2, p);
        SpiroGraph {
            fixed_gear : fixed_gear,
            rotating_gear : rotating_gear,
        }
    );

    // 毎フレーム、回転ギア座標から固定ギア座標への変換行列を出力する機械
    transform_machine : SpiroGraph -> Machine TimeDelta Matrix;
    transform_machine = |sg| clock.map(|t| sg.get_transform(t * velocity));

    // 軌跡を描画するアニメーション
    trajectory_animation : SpiroGraph -> Animation;
    trajectory_animation = |sg| (
        // 毎フレーム、ペン先の位置を出力する機械
        let pen_point_machine : Machine TimeDelta Vec2 = transform_machine(sg).map(|m|
            let p = sg.@rotating_gear.@point;
            Vec2::make_from_tuple $ m.transform_point(p.to_tuple)
        );
        // ペン先の情報を受け取ると、(前回のペン先, 今回のペン先) を出力する機械
        let stroke_machine : Machine Vec2 (Option (Vec2, Vec2)) = Machine::from_transition(none(), |now, opt_prev|
            match opt_prev {
                none() => (some(now), none()),
                some(prev) => (some(now), some((prev, now)))
            }
        );
        // 毎フレーム、前回のペン先から新しいペン先に向かって線を引くアニメーション
        let stroke_animation : Animation = pen_point_machine.compose(stroke_machine).map(|opt_prev_now, cairo|
            if opt_prev_now.is_none { pure() };
            let (prev, now) = opt_prev_now.as_some;
            cairo.set_source_rgba(line_color);;
            cairo.move_to(prev.to_tuple);;
            cairo.line_to(now.to_tuple);;
            cairo.stroke
        );
        // stroke_animationをバッファリングして軌跡のアニメーションにする
        let s = spirograph_size;
        let buffer_rect = Rectangle::make(-s, -s, 2.0*s, 2.0*s);
        stroke_animation.buffered(buffer_rect)
    );

    // 固定ギアと回転ギアを描画するアニメーション
    gears_animation : SpiroGraph -> Animation;
    gears_animation = |sg| (
        let fg = sg.@fixed_gear;
        let rg = sg.@rotating_gear;
        transform_machine(sg).map(|m, cairo|
            // 固定ギアを描く
            fg.render(cairo);; 

            // 回転ギアとペンを描く
            cairo.save;;
            cairo.transform(m);;
            // 回転ギア
            rg.render(cairo);;
            // ペン
            cairo.set_source_rgb(pen_color);;
            cairo.arc(rg.@point.to_tuple, 2.0, 0.0, 2.0 * pi64);;
            cairo.fill;;
            cairo.restore;;

            pure()
        )
    );

    // このスピログラフのアニメーション
    animation : SpiroGraph -> Animation;
    animation = |sg| Animation::stack([trajectory_animation(sg), gears_animation(sg)]);
}

type Scene = box struct {
    // 8個のスピログラフ
    spirographs : Array SpiroGraph,
    // アニメーションが開始した時刻
    start_time : F64,
    // 軌跡を描画するためのレイヤー
    layer : (Surface, Cairo),
};

spirographs : Array SpiroGraph;
spirographs = [
    SpiroGraph::two_circles_inner(spirograph_size / (4.0/1.0), spirograph_size / (4.55/1.0), Vec2::make(spirograph_size / (6.5/1.0), 0.0)),
    SpiroGraph::two_circles_inner(spirograph_size / 3.5, spirograph_size / 11.1, Vec2::make(spirograph_size / 12.0, 0.0)),
    SpiroGraph::two_circles_inner(spirograph_size / 7.5, spirograph_size / 4.05, Vec2::make(spirograph_size / 5.0, 0.0)),
    SpiroGraph::two_circles_inner(spirograph_size / 3.0, spirograph_size / 8.6, Vec2::make(spirograph_size / 15.0, 0.0)),

    SpiroGraph::circle_ellipse_inner(spirograph_size / (12.0/1.5), spirograph_size / (5.1/1.5), spirograph_size / (7.0/1.5), Vec2::make(spirograph_size / (9.0/1.5), 0.0)),
    SpiroGraph::circle_ellipse_inner(spirograph_size / 3.0, spirograph_size / 10.0, spirograph_size / 15.0, Vec2::make(spirograph_size / 19.0, 0.0)),
    SpiroGraph::circle_ellipse_inner(spirograph_size / 4.0, spirograph_size / 2.55, spirograph_size / 5.0, Vec2::make(spirograph_size / 5.5, 0.0)),
    SpiroGraph::circle_ellipse_inner(spirograph_size / 3.0, spirograph_size / 13.0, spirograph_size / 17.0, Vec2::make(spirograph_size / 19.0, 0.0)),
];

animation : Animation;
animation = (
    spirographs.to_iter.zip(count_up(0)).map(|(sg, i)| sg.animation.translate(spirograph_center(i))).to_array.Animation::stack
);

main : IO ();
main = (
    let config = Funim::Config::default(window_size);
    Funim::run_animation(animation, config)
);

/* ユーティリティ関数 */

to_string : Matrix -> String;
to_string = |m| (
    let ret = "";
    let ret = ret + m.@xx.to_string + " ";
    let ret = ret + m.@xy.to_string + " ";
    let ret = ret + m.@x0.to_string + "\n";
    let ret = ret + m.@yx.to_string + " ";
    let ret = ret + m.@yy.to_string + " ";
    let ret = ret + m.@y0.to_string;
    ret
);
